// @NOTE(steve): This was pretty easy to get running but the error messages you get when things don't parse aren't as
// good as what we want to expose to polar users. Either we do a lot of mapping or we replace this with a hand rolled
// parser later.

use std::str::FromStr;
use std::collections::{HashMap, BTreeMap};

use crate::lexer::{self, Token};
use crate::parser::Line;
use crate::error;
use crate::types::*;

use lalrpop_util::ParseError;

grammar(src_id: u64);

extern {
    type Location = usize;
    type Error = error::ParseError;

    enum Token {
        "Integer" => lexer::Token::Integer(<i64>),
        "Float" => lexer::Token::Float(<f64>),
        "String" => lexer::Token::String(<String>),
        "Boolean" => lexer::Token::Boolean(<bool>),
        "Symbol" => lexer::Token::Symbol(<Symbol>),
        ":" => lexer::Token::Colon,     // :
        "," => lexer::Token::Comma,     // ,
        "[" => lexer::Token::LB,        // [
        "]" => lexer::Token::RB,        // ]
        "(" => lexer::Token::LP,        // (
        ")" => lexer::Token::RP,        // )
        "{" => lexer::Token::LCB,       // {
        "}" => lexer::Token::RCB,       // }
        "." => lexer::Token::Dot,       // .
        "new" => lexer::Token::New,     // new
        "!" => lexer::Token::Bang,       // !
        "*" => lexer::Token::Mul,       // *
        "/" => lexer::Token::Div,       // /
        "+" => lexer::Token::Add,       // +
        "-" => lexer::Token::Sub,       // -
        "==" => lexer::Token::Eq,       // ==
        "!=" => lexer::Token::Neq,      // !=
        "<=" => lexer::Token::Leq,      // <=
        ">=" => lexer::Token::Geq,      // >=
        "<" => lexer::Token::Lt,        // <
        ">" => lexer::Token::Gt,        // >
        "=" => lexer::Token::Unify,     // =
        "|" => lexer::Token::Pipe,      // |
        ";" => lexer::Token::SemiColon, // ;
        ":=" => lexer::Token::Define,   // :=
        "?=" => lexer::Token::Query,    // ?=
        "cut" => lexer::Token::Cut,
        "debug" => lexer::Token::Debug,
        "print" => lexer::Token::Print,
        "in" => lexer::Token::In,       // in
        "forall" => lexer::Token::ForAll,     // forall
        "if" => lexer::Token::If,       // if
        "and" => lexer::Token::And,     // and
        "or" => lexer::Token::Or,       // or
        "not" => lexer::Token::Not,       // not
        "matches" => lexer::Token::Matches,   // matches
    }
}


// ****** Values ******* //

Integer: i64 = {
    <"Integer">,
"+" <"Integer">,
"-" <i:"Integer"> => -i
}

Float: f64 = {
    <"Float">,
"+" <"Float">,
"-" <f:"Float"> => -f
}


Number: Value = {
    <Integer> => Value::Number(<>.into()),
    <Float> => Value::Number(<>.into()),
};


PolarString: Value = <s:"String"> => {
    Value::String(s)
};

Boolean: Value = <b:"Boolean"> => {
    Value::Boolean(b)
};

Name: Symbol = <s:"Symbol"> => s;

Variable: Value  = <n:Name> => {
    Value::Variable(n)
};

Predicate: Value = {
    <name:Name> "(" <mut args:(<TermExp> ",")*> <arg:TermExp?> ")" => {
        match arg {
            Some(arg) => args.push(arg),
            None => ()
        };
        Value::Call(Predicate{name, args})
    }
};

Fields<T>: BTreeMap<Symbol, Term> = {
    <name:Name> ":" <value:T> => {
        let mut fields = BTreeMap::new();
        fields.insert(name, value);
        fields
    },
    <name:Name> ":" <value:T> "," <mut fields:Fields<T>> => {
        fields.insert(name, value);
        fields
    }
};

Object<T>: Dictionary = {
    "{" <fields:Fields<T>> "}" => {
        Dictionary { fields }
    },
    "{" "}" => {
        Dictionary {
            fields: BTreeMap::new()
        }
    }
};

// ****** Strange Values ******* //

DictionaryTerm: Value = <fields:Object<Exp5<"Term">>> => {
    Value::Dictionary(fields)
};
// Pattern dictionaries cannot contain any operators.
DictionaryPattern: Value = <fields:Object<Exp9<"Pattern">>> => {
    Value::Pattern(Pattern::Dictionary(fields))
};

InstanceLiteralTerm: Value = <tag:Name> <fields:Object<Exp5<"Term">>> => {
    let instance = InstanceLiteral{tag, fields };
    Value::InstanceLiteral(instance)
};

InstanceLiteralPattern: Value = <tag:Name> <fields:Object<Exp9<"Pattern">>> => {
    let instance = InstanceLiteral{tag, fields};
    Value::Pattern(Pattern::Instance(instance))
};

// ****** Operations ******* //

BuiltinOperator: Operator = {
    "debug" => Operator::Debug,
    "print" => Operator::Print,
};

BuiltinOperation: Value = {
    <op:BuiltinOperator> "(" <mut args:(<TermExp> ",")*> <arg:TermExp?> ")" => {
        match arg {
            Some(arg) => args.push(arg),
            None => ()
        };
        Value::Expression(Operation{operator: op, args: args})
    },
};

RewritableOperator: Operator = {
    "." => Operator::Dot,
    "new" => Operator::New,
    "in" => Operator::In,
};

RewrittenOperation: Value = {
    <op:RewritableOperator> "(" <mut args:(<TermExp> ",")*> <arg:TermExp?> ")" => {
        match arg {
            Some(arg) => args.push(arg),
            None => ()
        };
        Value::Expression(Operation{operator: op, args: args})
    },
};


New: Value = "new" <literal:Spanned<InstanceLiteralTerm>> => {
    let args = vec![literal];
    let op = Operation{operator: Operator::New, args};
    Value::Expression(op)
};

SpecialExp<T>: Value = {
    // A pattern cannot contain a new operator
    <New> if T == "Term",
    "cut" => {
        let args = vec![];
        let op = Operation{operator: Operator::Cut, args};
        Value::Expression(op)
    },
    "forall" "(" <arg1:TermExp> "," <arg2:TermExp> ")" => {
        let args = vec![arg1, arg2];
        let op = Operation{operator: Operator::ForAll, args};
        Value::Expression(op)
    },
}



// ****** Expressions ******* //

// All ExpN & Exp productions are macros with one parameter. The parameter is the
// *string* "Term" or "Pattern" which controls whether the expression is over terms
// or patterns.  (It is a string since we need to conditionally
// change the expression precedence allowed in patterns versus terms depending
// on the parameter type, and LALRPOP does not allow conditional macros on anything
// other than a string.

Exp<T>: Term = {
    <exp1:Exp1<T>> => exp1
}

pub TermExp: Term = {
    <Exp<"Term">> => <>
};

pub PatternExp: Term = {
    <Exp9<"Pattern">> => <>
};


Exp11<T>: Term = {
    <Spanned<Pattern>> if T == "Pattern",
    <Spanned<Value>> if T == "Term",
    "(" <Exp<T>> ")", // "resets" the parsing
}

Exp10<T>: Term = {
    <Spanned<SpecialExp<T>>>,
    <Exp11<T>>,
}

Callable<T>: Value = {
    <DictionaryTerm> if T == "Term" => <>,
    <DictionaryPattern> if T == "Pattern" => <>,
    <Variable> => <>,
    <DotOp<T>> => <>,
    // A pattern cannot contain a new operator.
    <New> if T == "Term" => <>, // TODO(Sam): should we actually allow this?
};

CallTerm: Value = {
    <Predicate>,
    <name:Name> => Value::Call(Predicate{name, args: vec![]}),
    "(" <Variable> ")",
}

DotOp<T>: Value = {
    <head:Spanned<Callable<T>>> "." <call_term:Spanned<CallTerm>> => {
        let args = vec![head, call_term];
        let op = Operation{operator: Operator::Dot, args};
        Value::Expression(op)
    },
}

// .
Exp9<T>: Term = {
    <Spanned<DotOp<T>>>,
    <Exp10<T>>,
};

// in
InExp<T>: Value = {
    <left:Exp8<T>> "in" <right:Exp9<T>> => {
        let args = vec![left, right];
        let op = Operation{operator: Operator::In, args};
        Value::Expression(op)
    },
}

Matches = {"matches"};
// matches
MatchExp<T>: Value = {
    // Symbols on the RHS are treated as class names, just like in a specializers
    <left:Exp8<T>> Matches <right:Spanned<Pattern>> => {
        let right = if let Value::Variable(ref sym) = right.value() {
            right.clone_with_value(Value::Pattern(Pattern::Instance(InstanceLiteral {
                tag: sym.clone(),
                fields: Dictionary::new()
            })))
        } else {
            right
        };
        let args = vec![left, right];
        let op = Operation{operator: Operator::Isa, args};
        Value::Expression(op)
    },
}

Exp8<T>: Term = {
    <Spanned<InExp<T>>>,
    <Spanned<MatchExp<T>>>,
    <Exp9<T>>,
}

// * /
Op7: Operator = {
    "*" => Operator::Mul,
    "/" => Operator::Div,
}
MulExp<T>: Value = {
    <exp7:Exp7<T>> <operator:Op7> <exp8:Exp8<T>> => {
        let args = vec![exp7, exp8];
        let op = Operation{operator, args};
        Value::Expression(op)
    },
}
Exp7<T>: Term = {
    <Spanned<MulExp<T>>>,
    <Exp8<T>>,
}

// + -
Op6: Operator = {
    "+" => Operator::Add,
    "-" => Operator::Sub,
}
AddExp<T>: Value = {
    <exp6:Exp6<T>> <operator:Op6> <exp7:Exp7<T>> => {
        let args = vec![exp6, exp7];
        let op = Operation{operator, args};
        Value::Expression(op)
    },
}

Exp6<T>: Term = {
    <Spanned<AddExp<T>>>,
    <Exp7<T>>,
}

// == != <= < >= >
Op5: Operator = {
    "==" => Operator::Eq,
    "!=" => Operator::Neq,
    "<=" => Operator::Leq,
    ">=" => Operator::Geq,
    "<" => Operator::Lt,
    ">" => Operator::Gt,
}

CmpExp<T>: Value = {
    <exp5:Exp5<T>> <operator:Op5> <exp6:Exp6<T>> => {
        let args = vec![exp5, exp6];
        let op = Operation{operator, args};
        Value::Expression(op)
    },
}

Exp5<T>: Term = {
    <Spanned<CmpExp<T>>>,
    <Exp6<T>>,
}

// =
UnifyExp<T>: Value = {
    <exp4:Exp4<T>> "=" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Unify, args};
        Value::Expression(op)
    },
}

Exp4<T>: Term = {
    <Spanned<UnifyExp<T>>>,
    <Exp5<T>>,
}


 // !
Not = {"not"};
NotExp<T>: Value = {
    Not <exp4:Exp4<T>> => {
        let args = vec![exp4];
        let op = Operation{operator: Operator::Not, args};
        Value::Expression(op)
    },
}

Exp3<T>: Term = {
    <Spanned<NotExp<T>>>,
    <Exp4<T>>,
}


Or = {"or"};
OrExp<T>: Value = {
    <head:Exp3<T>> Or <mut tail:Exp2<T>> => {
        let args = match &mut tail.value() {
            Value::Expression(Operation{operator: Operator::Or, args: tail_args}) => {
                let mut args = vec![head];
                args.append(&mut tail_args.clone());
                args
            }
            _ => {
                vec![head, tail]
            }
        };
        let op = Operation{operator: Operator::Or, args};
        Value::Expression(op)
    },
}

Exp2<T>: Term = {
    <Spanned<OrExp<T>>>,
    <Exp3<T>>,
}


And = {"and"};
AndExp<T>: Value = {
    <head:Exp2<T>> And <mut tail:Exp1<T>> => {
        let args = match &mut tail.value() {
            Value::Expression(Operation{operator: Operator::And, args: tail_args}) => {
                let mut args = vec![head];
                args.append(&mut tail_args.clone());
                args
            }
            _ => {
                vec![head, tail]
            }
        };
        let op = Operation{operator: Operator::And, args};
        Value::Expression(op)
    },
}

Exp1<T>: Term = {
    <Spanned<AndExp<T>>>,
    <Exp2<T>>,
}


pub Pattern: Value = {
    <Number>,
    <PolarString>,
    <Boolean>,
    <Variable>,
    <DictionaryPattern>,
    <InstanceLiteralPattern>,
    "[" <terms:(<Spanned<Pattern>> ",")*> <term:Spanned<RestVar<"Pattern">>?> "]" => {
        match term {
            Some(term) => Value::List(vec![term]),
            None => Value::List(terms)
        }
    },
};

pub Value: Value = {
    <Number>,
    <PolarString>,
    <Boolean>,
    <Variable>,
    <DictionaryTerm>,
    <BuiltinOperation>,
    <RewrittenOperation>,
    <Predicate>,
    "[" <mut terms:(<Term> ",")*> <term:Spanned<RestVar<"Term">>?> "]" => {
        match term {
            Some(term) => {
                terms.push(term);
                Value::List(terms)
            },
            None => Value::List(terms)
        }
    },
}



// ****** Terms + Patterns ******* //

// Either a *rest variable, or an ordinary term.
// TODO(Sam): come back to this one
RestVar<T>: Value = {
    "*" <Name> => Value::RestVariable(<>),
    <Value> if T == "Term",
    <Pattern> if T == "Pattern",
};



Spanned<T>: Term = <start:@L> <value:T> <end:@R> =>  Term::new_from_parser(src_id, start, end, value);

pub Term = TermExp;



// ****** Rules + Lines ******* //

ParameterList: Vec<Parameter> = {
    <param:Parameter> => vec![param],
    <mut list:ParameterList> "," <param:Parameter> => {
        list.push(param);
        list
    },
};


Parameter: Parameter = {
    <parameter:Exp6<"Term">> => {
        Parameter{parameter, specializer: None}
    },
    // parenthesized specializers do not have symbol translation to class names applied
    <parameter:Spanned<Variable>> ":" "(" <specializer:Spanned<Pattern>> ")" => {
        Parameter {
            parameter,
            specializer: Some(specializer),
        }
    },
    <parameter:Spanned<Variable>> ":" <specializer:Spanned<Pattern>> => {
        let offset = specializer.offset();
        if let Value::Variable(class_name) = specializer.value() {
            let fields = BTreeMap::new();
            let instance_literal = InstanceLiteral{tag: class_name.clone(), fields: Dictionary{fields}};
            Parameter {
                parameter,
                specializer: Some(specializer.clone_with_value(Value::Pattern(Pattern::Instance(instance_literal)))),
            }
        } else {
            Parameter{parameter, specializer: Some(specializer)}
        }
    },
};


RuleHead: (Symbol, Vec<Parameter>) = {
    <name:Name> "(" ")" => {
        (name, vec![])
    },
    <name:Name> "(" <params:ParameterList> ")" => {
        (name, params)
    }
};

Define = {":=", "if"};

pub Rule: Rule = {
    <head:RuleHead> <start:@L> <end:@R> ";" => {
        let (name, params) = head;
        let op = Operation{operator: Operator::And, args: vec![]};
        let body = Term::new_from_parser(src_id, start, end, Value::Expression(op));
        Rule{name, params, body}
    },
    <head:RuleHead> Define <body:TermExp> ";" => {
        let (name, params) = head;
        let body = match body.value() {
            Value::Expression(Operation{operator: Operator::And, ..}) => {
                body
            },
            _ => {
                let op = Operation{operator: Operator::And, args: vec![body.clone()]};
                body.clone_with_value(Value::Expression(op))
            }
        };
        Rule{name, params, body}
    }
}


pub Rules: Vec<Rule> = <Rule*>;

Line: Line = {
    <Rule> => Line::Rule(<>),
    "?=" <TermExp> ";" => Line::Query(<>),
}

pub Lines: Vec<Line> = <Line*>;
