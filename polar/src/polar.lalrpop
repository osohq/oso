// @NOTE(steve): This was pretty easy to get running but the error messages you get when things don't parse aren't as
// good as what we want to expose to polar users. Either we do a lot of mapping or we replace this with a hand rolled
// parser later.

use std::str::FromStr;
use std::collections::{HashMap, BTreeMap};

use crate::lexer::{self, Token};
use crate::parser::Line;
use crate::error;
use crate::types::*;

use lalrpop_util::ParseError;

grammar;

extern {
    type Location = usize;
    type Error = error::ParseError;

    enum Token {
        "Integer" => lexer::Token::Integer(<i64>),
        "Float" => lexer::Token::Float(<f64>),
        "String" => lexer::Token::String(<String>),
        "Boolean" => lexer::Token::Boolean(<bool>),
        "Symbol" => lexer::Token::Symbol(<Symbol>),
        ":" => lexer::Token::Colon,     // :
        "," => lexer::Token::Comma,     // ,
        "[" => lexer::Token::LB,        // [
        "]" => lexer::Token::RB,        // ]
        "(" => lexer::Token::LP,        // (
        ")" => lexer::Token::RP,        // )
        "{" => lexer::Token::LCB,       // {
        "}" => lexer::Token::RCB,       // }
        "." => lexer::Token::Dot,       // .
        "new" => lexer::Token::New,     // new
        "!" => lexer::Token::Not,       // !
        "*" => lexer::Token::Mul,       // *
        "/" => lexer::Token::Div,       // /
        "+" => lexer::Token::Add,       // +
        "-" => lexer::Token::Sub,       // -
        "==" => lexer::Token::Eq,       // ==
        "!=" => lexer::Token::Neq,      // !=
        "<=" => lexer::Token::Leq,      // <=
        ">=" => lexer::Token::Geq,      // >=
        "<" => lexer::Token::Lt,        // <
        ">" => lexer::Token::Gt,        // >
        "=" => lexer::Token::Unify,     // =
        "|" => lexer::Token::Pipe,      // |
        ";" => lexer::Token::SemiColon, // ;
        ":=" => lexer::Token::Define,   // :=
        "?=" => lexer::Token::Query,    // ?=
        "cut" => lexer::Token::Cut,
        "debug" => lexer::Token::Debug,
        "in" => lexer::Token::In,       // in
        "isa" => lexer::Token::Isa,     // isa
    }
}

pub Number: Term = {
    <start:@L> <i:"Integer"> => {
        Term{id: 0, offset: start, value: Value::Number(i.into())}
    },
    <start:@L> <f:"Float"> => {
        Term{id: 0, offset: start, value: Value::Number(f.into())}
    }
};


pub PolarString: Term = <start:@L> <s:"String"> => {
    Term{id: 0, offset: start, value: Value::String(s)}
};

pub Boolean: Term = <start:@L> <b:"Boolean"> => {
    Term{id: 0, offset: start, value: Value::Boolean(b)}
};

Name: Symbol = <s:"Symbol"> => s;

pub Symbol: Term  = <start:@L> <n:Name> => {
    Term{id: 0, offset: start, value: Value::Symbol(n)}
};

Fields<T>: BTreeMap<Symbol, Term> = {
    <name:Name> ":" <value:Exp5<T>> => {
        let mut fields = BTreeMap::new();
        fields.insert(name, value);
        fields
    },
    <name:Name> ":" <value:Exp5<T>> "," <mut fields:Fields<T>> => {
        fields.insert(name, value);
        fields
    }
};

Object<T>: BTreeMap<Symbol, Term> = {
    "{" <fields:Fields<T>> "}" => {
        fields
    },
    "{" "}" => {
        BTreeMap::new()
    }
};

Dictionary<T>: Term = <start:@L> <fields:Object<T>> => {
    let dict = Dictionary{fields};
    Term{id: 0, offset: start, value: Value::Dictionary(dict)}
};

InstanceLiteral<T>: Term = <start:@L> <tag:Name> <fields:Object<T>> => {
    let instance = InstanceLiteral{tag, fields: Dictionary{fields}};
    Term{id: 0, offset: start, value: Value::InstanceLiteral(instance)}
};

pub InstanceLiteralTerm: Term = <InstanceLiteral<Term>> => <>;

pub Pattern: Term = {
    <term:InstanceLiteral<Pattern>> => Pattern::term_as_pattern(&term),
    <term:Dictionary<Pattern>> => Pattern::term_as_pattern(&term),
    <term:Number> => term,
    <term:PolarString> => term,
    <term:Boolean> => term,
    <term:Symbol> => term,
    <start:@L> <op:RewrittenOperation> => Term{id: 0, offset: start, value: Value::Expression(op)},
    <start:@L> <pred:Predicate> => Term{id: 0, offset: start, value: Value::Call(pred)},
    <start:@L> "[" "]" => Term{id: 0, offset: start, value: Value::List(vec![])},
    <start:@L> "[" <exp:PatternExp> "]" => {
        let terms = unwrap_and(exp);
        Term{id:0, offset: start, value: Value::List(terms)}
    }
};

pub Term: Term = {
    <term:Number> => term,
    <term:PolarString> => term,
    <term:Boolean> => term,
    <term:Symbol> => term,
    <term:Dictionary<Term>> => term,
    <start:@L> <op:RewrittenOperation> => Term{id: 0, offset: start, value: Value::Expression(op)},
    <start:@L> <pred:Predicate> => Term{id: 0, offset: start, value: Value::Call(pred)},
    <start:@L> "[" "]" => Term{id: 0, offset: start, value: Value::List(vec![])},
    <start:@L> "[" <exp:TermExp> "]" => {
        let terms = unwrap_and(exp);
        Term{id:0, offset: start, value: Value::List(terms)}
    }
};

RewritableOperator: Operator = {
    "." => Operator::Dot,
    "new" => Operator::New,
    "in" => Operator::In,
    "debug" => Operator::Debug,
};

pub RewrittenOperation: Operation = {
    <op:RewritableOperator> "(" <args:TermExp> ")" => {
        let args = unwrap_and(args);
        Operation{operator: op, args: args}
    },
    <op:RewritableOperator> "(" ")" => {
        Operation{operator: op, args: vec![]}
    }
};

pub Predicate: Predicate = {
    <name:Name> "(" ")" => {
        let args = vec![];
        Predicate{name, args}
    },
    <name:Name> "(" <args:TermExp> ")" => {
        let args = unwrap_and(args);
        Predicate{name, args}
    }
};

Exp11<T>: Term = {
    <term:T> => term,
    "(" <exp:Exp<T>> ")" => exp // seems sorta impossible to support parrens for expressions and list literals.
}

Exp10<T>: Term = {
    <new:New> => new,
    <start:@L> "cut" "(" ")" => {
        let args = vec![];
        let op = Operation{operator: Operator::Cut, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> "cut" => {
        let args = vec![];
        let op = Operation{operator: Operator::Cut, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp11:Exp11<T>> => exp11,
}

New: Term = <start:@L> "new" <literal:InstanceLiteral<Term>> => {
    let args = vec![literal];
    let op = Operation{operator: Operator::New, args};
    Term{id: 0, offset: start, value: Value::Expression(op)}
};

// @TODO: Think about this.
Callable<T>: Term = {
    <Dictionary<T>> => <>,
    <Symbol> => <>,
    <DotOp<T>> => <>,
    <New> => <>,
};

DotOp<T>: Term = {
    <start:@L> <head:Callable<T>> "." <call_loc:@L> <mut call:Predicate> => {
        let call_term = Term{id: 0, offset: call_loc, value: Value::Call(call)};
        let args = vec![head, call_term];
        let op = Operation{operator: Operator::Dot, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <head:Callable<T>> "." <call_loc:@L> <name:Name> => {
        let call = Predicate{name, args: vec![]};
        let call_term = Term{id: 0, offset: call_loc, value: Value::Call(call)};
        let args = vec![head, call_term];
        let op = Operation{operator: Operator::Dot, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <head:Callable<T>> "." <call_loc:@L> "(" <name:Name> ")" => {
        let call_term = Term{id: 0, offset: call_loc, value: Value::Symbol(name)};
        let args = vec![head, call_term];
        let op = Operation{operator: Operator::Dot, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    }
}

// .
Exp9<T>: Term = {
    <dot_op:DotOp<T>> => dot_op,
    <exp10:Exp10<T>> => exp10
};

// in isa
Exp8<T>: Term = {
    <start:@L> <left:Exp8<T>> "in" <right:Exp9<T>> => {
        let args = vec![left, right];
        let op = Operation{operator: Operator::In, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    // Symbols on the RHS are treated as class names, just like in a specializers
    <start:@L> <left:Exp8<T>> "isa" <right:Exp9<Pattern>> => {
        let right = if let Value::Symbol(ref sym) = right.value {
            right.clone_with_value(Value::Pattern(Pattern::Instance(InstanceLiteral {
                tag: sym.clone(),
                fields: Dictionary::new()
            })))
        } else {
            Pattern::term_as_pattern(&right)
        };
        let args = vec![left, right];
        let op = Operation{operator: Operator::Isa, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp9:Exp9<T>> => exp9
}

// !
Exp7<T>: Term = {
    <start:@L> "!" <exp8:Exp8<T>> => {
        let args = vec![exp8];
        let op = Operation{operator: Operator::Not, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    // TODO (dhatch): Maybe a different type here
    <exp8:Exp8<T>> => exp8
}

// * /
Exp6<T>: Term = {
    <start:@L> <exp6:Exp6<T>> "*" <exp7:Exp7<T>> => {
        let args = vec![exp6, exp7];
        let op = Operation{operator: Operator::Mul, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp6:Exp6<T>> "/" <exp7:Exp7<T>> => {
        let args = vec![exp6, exp7];
        let op = Operation{operator: Operator::Div, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp7:Exp7<T>> => exp7
}

// + -
Exp5<T>: Term = {
    <start:@L> <exp5:Exp5<T>> "+" <exp6:Exp6<T>> => {
        let args = vec![exp5, exp6];
        let op = Operation{operator: Operator::Add, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp5:Exp5<T>> "-" <exp6:Exp6<T>> => {
        let args = vec![exp5, exp6];
        let op = Operation{operator: Operator::Sub, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp6:Exp6<T>> => exp6
}

// == != <= < >= >
Exp4<T>: Term = {
    <start:@L> <exp4:Exp4<T>> "==" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Eq, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp4:Exp4<T>> "!=" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Neq, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp4:Exp4<T>> "<=" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Leq, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp4:Exp4<T>> ">=" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Geq, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp4:Exp4<T>> "<" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Lt, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <start:@L> <exp4:Exp4<T>> ">" <exp5:Exp5<T>> => {
        let args = vec![exp4, exp5];
        let op = Operation{operator: Operator::Gt, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp5:Exp5<T>> => exp5
}

// =
Exp3<T>: Term = {
    <start:@L> <exp3:Exp3<T>> "=" <exp4:Exp4<T>> => {
        let args = vec![exp3, exp4];
        let op = Operation{operator: Operator::Unify, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
     <exp4:Exp4<T>> => exp4
 }

// |
Exp2<T>: Term = {
    <start:@L> <head:Exp3<T>> "|" <mut tail:Exp2<T>> => {
        let args = match &mut tail {
            Term{value: Value::Expression(Operation{operator: Operator::Or, args: tail_args}), ..} => {
                let mut args = vec![head];
                args.append(tail_args);
                args
            }
            _ => {
                vec![head, tail]
            }
        };
        let op = Operation{operator: Operator::Or, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp3:Exp3<T>> => exp3
}

// ,
Exp1<T>: Term = {
    <start:@L> <head:Exp2<T>> "," <mut tail:Exp1<T>> => {
        let args = match &mut tail {
            Term{value: Value::Expression(Operation{operator: Operator::And, args: tail_args}), ..} => {
                let mut args = vec![head];
                args.append(tail_args);
                args
            }
            _ => {
                vec![head, tail]
            }
        };
        let op = Operation{operator: Operator::And, args};
        Term{id: 0, offset: start, value: Value::Expression(op)}
    },
    <exp2:Exp2<T>> => exp2
}

Exp<T>: Term = {
    <exp1:Exp1<T>> => exp1
}

Parameter: Parameter = {
    <specializer:Exp5<Term>> => {
        if let Term{value: Value::Symbol(_), ..} = specializer {
            Parameter{parameter: Some(specializer), specializer: None}
        } else {
            Parameter{parameter: None, specializer: Some(specializer)}
        }
    },
    // parenthesized specializers can be arbitrary expressions
    <name:Name> ":" "(" <specializer:TermExp> ")" => {
        Parameter {
            parameter: Some(Term::new(Value::Symbol(name))),
            specializer: Some(specializer),
        }
    },
    // unparenthesized specializers can only be terms
    // and symbols are interpreted as class names
    <name:Name> ":" <specializer:Term> => {
        let offset = specializer.offset;
        if let Term{value: Value::Symbol(class_name), ..} = specializer {
            let fields = BTreeMap::new();
            let instance_literal = InstanceLiteral{tag: class_name, fields: Dictionary{fields}};
            Parameter{parameter: Some(Term::new(Value::Symbol(name))), specializer: Some(Term{id: 0, offset, value: Value::InstanceLiteral(instance_literal)})}
        } else {
            Parameter{parameter: Some(Term::new(Value::Symbol(name))), specializer: Some(specializer)}
        }
    },
    <name:Name> ":" <specializer:InstanceLiteral<Term>> => {
        Parameter{parameter: Some(Term::new(Value::Symbol(name))), specializer: Some(specializer)}
    },
    <specializer:InstanceLiteral<Term>> => {
        Parameter{parameter: None, specializer: Some(specializer)}
    },
};

pub TermExp: Term = {
    <Exp<Term>> => <>
};

pub PatternExp: Term = {
    <Exp<Pattern>> => <>
};

ParameterList: Vec<Parameter> = {
    <param:Parameter> => vec![param],
    <mut list:ParameterList> "," <param:Parameter> => {
        list.push(param);
        list
    },
};

RuleHead: (Symbol, Vec<Parameter>) = {
    <name:Name> "(" ")" => {
        (name, vec![])
    },
    <name:Name> "(" <params:ParameterList> ")" => {
        (name, params)
    }
};

pub Rule: Rule = {
    <head:RuleHead> <start:@L> ";" => {
        let (name, params) = head;
        let op = Operation{operator: Operator::And, args: vec![]};
        let body = Term{id: 0, offset: start, value: Value::Expression(op)};
        Rule{name, params, body}
    },
    <head:RuleHead> ":=" <body:TermExp> ";" => {
        let (name, params) = head;
        let body = match body {
            Term{value: Value::Expression(Operation{operator: Operator::And, ..}), ..} => {
                body
            },
            _ => {
                let offset = body.offset.clone();
                let op = Operation{operator: Operator::And, args: vec![body]};
                Term{id: 0, offset, value: Value::Expression(op)}
            }
        };
        Rule{name, params, body}
    }
}

pub Rules: Vec<Rule> = <Rule*>;

Line: Line = {
    <Rule> => Line::Rule(<>),
    "?=" <TermExp> ";" => Line::Query(<>),
}

pub Lines: Vec<Line> = <Line*>;
